begin
  userPage()
  cls()

  // Vincular I/O
  dioLink(dPB[0], io:bIn4)   
  dioLink(dPB[1], io:bIn5)   
  dioLink(dPB[2], io:bIn6)   
  dioLink(dPB[3], io:bIn7)   

  dioLink(dLed[0], io:bOut4)
  dioLink(dLed[1], io:bOut5)
  dioLink(dLed[2], io:bOut6)
  dioLink(dLed[3], io:bOut7)

  // Velocitats
  velLenta.vel = 10
  velLenta.rvel = 20
  velLenta.accel = 10
  velLenta.decel = 10

  velRapida.vel = 30
  velRapida.accel = 30
  velRapida.decel = 30

  // Paràmetres de moviment
  nMilimetres = 5
  nGraus = 5

  putln("Inici del programa")
  // Anar a posició inicial
  movej(jHome, tPunxa, velRapida)
  movej(jWork, tPunxa, velRapida)
  movel(pInsercio[0], tPunxa, velLenta)
  movel(pInsercio[0], tPivot, velLenta)
  waitEndMove()

  putln("Esperant ordres")
  // Bucle de control amb botons
  while(dPB[3] == false)
    
    // Entrar
    if dPB[0] == true
      dLed[0] = true
      offset = {0, 0, nMilimetres, 0, 0, 0}
      pInsercio[1] = here(tPivot, fPacient)
      pInsercio[2] = appro(pInsercio[1], offset)
      movel(pInsercio[2], tPivot, velLenta)
      waitEndMove()
      dLed[0] = false
    endIf

    // Sortir
    if dPB[1] == true
      dLed[1] = true
      offset = {0, 0, -nMilimetres, 0, 0, 0}
      pInsercio[1] = here(tPivot, fPacient)
      pInsercio[2] = appro(pInsercio[1], offset)
      movel(pInsercio[2], tPivot, velLenta)
      waitEndMove()
      dLed[1] = false
    endIf

    // Pivotar (rotació en Y mantenint la punta)
    if dPB[2] == true
      dLed[2] = true
      rotacio = {0, 0, 0, 0, nGraus, 0}
      pInsercio[1] = here(tPivot, fPacient)
      pInsercio[2] = appro(pInsercio[1], rotacio)
      // Conservar posició XYZ fixa
      pInsercio[2].trsf.x = pInsercio[1].trsf.x
      pInsercio[2].trsf.y = pInsercio[1].trsf.y
      pInsercio[2].trsf.z = pInsercio[1].trsf.z
      movel(pInsercio[2], tPivot, velLenta)
      waitEndMove()
      dLed[2] = false
    endIf

  endWhile

  putln("Final del bucle. Tornant a home.")
  movel(pInsercio[0], tPivot, velLenta)
  movel(pInsercio[0], tPunxa, velLenta)
  movej(jWork, tPunxa, velRapida)
  movej(jHome, tPunxa, velRapida)
  waitEndMove()
end
